apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.1.0"
def siteUrl = 'https://github.com/shiftconnects/android-push-manager'      // Homepage URL of the library
def gitUrl = 'https://github.com/shiftconnects/android-push-manager.git'   // Git repository URL
def projectDesc = 'Provides a mechanism for quickly getting Google Cloud Messaging running within an Android app.'
group = "com.shiftconnects.android.push"

Properties properties = new Properties()
File file = project.rootProject.file('bintray.properties');
if (file.exists()) {
    properties.load(file.newDataInputStream())
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.google.android.gms:play-services-gcm:7.8.0'
    compile 'com.android.support:support-annotations:23.0.0'
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    def gpgPhrase = properties.getProperty("bintray.gpg.passphrase")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = group
        desc = projectDesc
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true

        version {
            vcsTag = version
            gpg {
                sign = true
                passphrase = gpgPhrase
            }
            mavenCentralSync {
                sync = true
                user = properties.getProperty("oss.userToken")
                password = properties.getProperty("oss.userTokenValue")
                close = '1'
            }
        }
    }
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'Android Push Manager'
                description projectDesc
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'mattkranzler5'
                        name 'Matt Kranzler'
                        email 'matt@mattkranzler.org'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
